/*
 * SMS Import / Export: a simple Android app for importing and exporting SMS and MMS messages,
 * call logs, and contacts, from and to JSON / NDJSON files.
 *
 * Copyright (c) 2021-2025 Thomas More
 *
 * This file is part of SMS Import / Export.
 *
 * SMS Import / Export is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * SMS Import / Export is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with SMS Import / Export.  If not, see <https://www.gnu.org/licenses/>
 *
 */

import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'org.jetbrains.kotlin.plugin.serialization' version '2.2.21'
}

android {
    compileSdk = 36

    defaultConfig {
        applicationId "com.github.tmo1.sms_ie"
        minSdkVersion 19
        targetSdkVersion 36
        versionCode 112
        versionName "2.7.0"
        base.archivesName = applicationId + '-v' + versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }

    // Based on https://developer.android.com/build/build-variants#flavor-dimensions
    flavorDimensions = ["api"]
    productFlavors {
        standard {
            dimension "api"
            minSdkVersion 21
            versionCode 10000 + android.defaultConfig.versionCode
            versionNameSuffix "-standard"
            getIsDefault().set(true)
        }
        legacy {
            dimension "api"
            minSdkVersion 19
            versionNameSuffix "-legacy"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlin {
        compilerOptions {
            jvmTarget = JvmTarget.JVM_17
        }

    }
    packagingOptions {
        resources {
            excludes += ['DebugProbesKt.bin']
        }
    }
    namespace 'com.github.tmo1.sms_ie'
    buildFeatures {
        buildConfig true
    }

}

dependencies {

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.10.2'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.9.0"

    standardImplementation 'androidx.core:core-ktx:1.17.0'
    standardImplementation 'com.google.android.material:material:1.13.0'
    standardImplementation 'androidx.documentfile:documentfile:1.1.0'
    standardImplementation 'androidx.appcompat:appcompat:1.7.1'
    standardImplementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    standardImplementation 'androidx.work:work-runtime-ktx:2.10.5'

    legacyImplementation 'androidx.core:core-ktx:1.13.1'
    legacyImplementation 'com.google.android.material:material:1.12.0'
    legacyImplementation 'androidx.appcompat:appcompat:1.6.1'
    legacyImplementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    legacyImplementation 'androidx.work:work-runtime-ktx:2.9.1'

    testImplementation 'junit:junit:4.13.2'

    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'

}
